(use* core)
(use* varvara)

(struct Maybe0 (Any) [value $0])

(word maybe? ((AnyOf Maybe0) -- Bool) [
	:value 0<>
])
(test maybe? [ 0 (make (Of Maybe0 U8)) maybe? (should eq nil) ])

(word maybe-not? ((AnyOf Maybe0) -- Bool) [
	:value 0=
])
(test maybe-not? [ 0 (make (Of Maybe0 U8)) maybe-not? (should eq t) ])

(word unwrap ((AnyOf Maybe0) -- (FieldType $0 value)) [
       :value
       dup 0= (when [ "Unwrapped 0 value" panic ])
])

(word orelse ((AnyOf Maybe0) (FieldType $1 value) -- (FieldType $1 value)) [
       swap :value dup 0= (when [ drop ] [ nip ])
])
(test orelse [ 0 (make (Of Maybe0 U8)) 12 orelse (should eq 12) ])

(word strlen ( @[Char8] -- U16 ) [ # TODO: move to str module
	0s swap
	(while [ -> 0<> ] [ swap 1+ swap 1+ ])
	drop
])
(test strlen [
	""                    strlen (should eq  0s)
	"Glory to the Empire" strlen (should eq 19s)
	"Hello, world!"       strlen (should eq 13s)
])

# TODO: cleanup once always-inline directive and (r) are added
# ( len src* dst* -- )
(word memcpy ( U16 $0 AnyPtr -- ) [
	#move
	(as @U8) swap (as @U8) swap
	(asm "s" .Op/Osth)

	dup rot                        # ( src src len      | dst* )
	(sizeof (Child $0)) (as U16) * # ( src src len*sz   | dst* )
	swap + swap                    # ( src*+len*sz src* | dst* )

	(while [ drop 2dup <> ] [
		dup -> (asm "srk" .Op/Osth) <- #dup -> (r copy) <-
		1+
		(asm "sr" .Op/Oinc) # (r 1+)
	])
	
	(asm "sr" .Op/Odrop)
	drop drop
])
(test memcpy [
	(var foo [Char8 11])
	'X 10s @foo + <-

	"Hail FORTH" dup strlen swap @foo memcpy
	0s @foo + -> (should eq 'H)
	1s @foo + -> (should eq 'a)
	2s @foo + -> (should eq 'i)
	3s @foo + -> (should eq 'l)
	4s @foo + -> (should eq  0x20)
	5s @foo + -> (should eq 'F)
	6s @foo + -> (should eq 'O)
	7s @foo + -> (should eq 'R)
	8s @foo + -> (should eq 'T)
	9s @foo + -> (should eq 'H)

	10s @foo + -> (should eq 'X)
])

(word panic (@U8 -- ) [
	"Panic: " print-string print-string nl
	.System/rst (asm "" .Op/Odei) 2 /
	(while [ 0> ] [
		"  at " print-string
		(asm "sr" .Op/Osth) print nl
		1-
	])
	"Aborting.\n" print-string
	(asm "" .Op/Ohalt)
])
