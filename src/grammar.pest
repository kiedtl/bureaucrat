program = _{ SOI ~ (word_decl|term)* ~ EOI}

word_decl = {
        "word" ~ ident ~ ":" ~ term* ~ ";;"
}

value = _{ float|string|character|table|quote|reference }
term = _{
	  float | string | character | table
	| quote | guard  | reference
	| fetch | store  | word
}

quote = { "[" ~ term* ~ "]" }

fetch = { "@" ~ ident }
store = { "!" ~ ident }
reference = { "&" ~ ident }

// TODO: integers in other bases (e.g. 0x10)
int   = _{ ASCII_DIGIT ~ (ASCII_DIGIT | "_")* }
exp   = _{ ^"e" ~ ("+" | "-")? ~ int }
float = @{
	( "-"? ~ int ~ "." ~ int? ~ exp? |
	  "-"? ~ int ~ exp? |
	  "-"? ~ int
	) ~ forced_ws
}

// TODO: use '' for char, "" for string
string = @{ "'" ~ ( "''" | (!"'" ~ ANY) )* ~ "'" }
character = @{ "$" ~ (!WHITESPACE ~ ANY) }

table = { ("{{" ~ table_keyval* ~ "}}")|("{" ~ table_val* ~ "}")}
table_keyval = { value ~ value }
table_val = { value }

guard = { "<" ~ guard_set ~ "-" ~ guard_set ~ ">" }
guard_set  = { guard_item* }
guard_item = { "a" | "s" | "n" | "q" | "*" }

word  = @{ !RESERVED ~ (!WHITESPACE ~ ANY)+ }
ident = @{ !RESERVED ~ (!WHITESPACE ~ ANY)+ }

forced_ws = _{ WHITESPACE+ | EOI }
RESERVED  = _{ "word" | ";;" | ":" | "[" | "]" | "{" | "}" }

COMMENT    = _{ ("(" ~ (COMMENT|(!")" ~ ANY))* ~ ")") | ("\\" ~ (!"\n" ~ ANY)*) }
WHITESPACE = _{ " " | "\t" | "\n" }
